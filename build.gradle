buildscript {
    ext.kotlinVersion = '1.6.10'
    repositories {
        google()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.2.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath("org.jmailen.gradle:kotlinter-gradle:2.1.3")
        classpath "org.owasp:dependency-check-gradle:7.1.2"
        classpath "com.cookpad.android.licensetools:license-tools-plugin:1.7.0"
        if (project.file("${rootProject.projectDir}/util/sonarqube.gradle").exists()) {
            classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:3.1.1"
        }
    }
}

plugins {
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

ext {
    projectGroup = 'com.sudoplatform'
}

if(project.file("${rootProject.projectDir}/util/internal-repo.gradle").exists()) {
    apply from: "${rootProject.projectDir}/util/internal-repo.gradle"
}
else {
    ext["internalRepoUsername"] = ""
    ext["internalRepoPassword"] = ""
    ext["internalSnapshotRepoUrl"] = ""
    ext["internalSnapshotRepoName"] = ""
    ext["internalReleaseRepoUrl"] = ""
    ext["internalReleaseRepoName"] = ""
    ext["sonatypeStagingProfileId"] = ""
}

if (project.findProperty("tag") && sonatypeStagingProfileId) {
    nexusPublishing {
        repositories {
            sonatype {
                stagingProfileId = sonatypeStagingProfileId

                username = nexusUsername
                password = nexusPassword
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}
